buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android-library'

android {
    compileSdkVersion 19
    buildToolsVersion '19'
    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 19
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']

        }
    }

}

apply plugin: 'maven'
apply plugin: 'signing'

version = '1.0.5'
group = 'com.andreabaccega'

configurations {
    archives {
        extendsFrom configurations.default
    }
}


signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'Android Form Edittext'
            packaging 'aar'
            artifactId 'android-form-edittext'
            description 'An extension of EditText with data validation facilities'
            url 'https://github.com/vekexasia/android-form-edittext'

            scm {
                url 'scm:git@github.com:vekexasia/android-form-edittext.git'
                connection 'scm:git@github.com:vekexasia/android-form-edittext.git'
                developerConnection 'scm:git@github.com:vekexasia/android-form-edittext.git'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'http://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'vekexasia'
                    name 'Andrea Baccega'
                    email 'me@andreabaccega.com'
                }
            }
        }
    }
}

